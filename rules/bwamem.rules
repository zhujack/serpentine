__author__  = "Johannes KÃ¶ster (http://johanneskoester.bitbucket.org)"
__license__ = "MIT"

### bwa.rules

rule bwamem_map:
    """Map individual units with bwa mem

    This rule uses the reference genome, after index creation,
    and maps the reads. Read group info is added automatically
    (LB, SM, PL, and ID).  Output is a coordinate-sorted BAM file.
    The output file is marked as temp().
    """
    input:
        index=lambda wildcards: SERPENTINE_HOME+"/resources/mapping/bwaindex_"+ config['bwaVersion'] +"/"+config['reference']+".pac",
        fastq=lambda wildcards: config["units"][wildcards.unit]
    output:
        temp("TEMP/{unit}.unit.bam")
    version: config['bwaVersion']
    params:
        batch ="-l nodes=1:gpfs -q ccr",
        sample =lambda wildcards: UNIT_TO_SAMPLE[wildcards.unit],
        library=lambda wildcards: UNIT_TO_LIBRARY[wildcards.unit],
        platform=config.get("platform","Illumina"),
        bwa_index = lambda wildcards: SERPENTINE_HOME + "/resources/mapping/bwaindex_" + config['bwaVersion'] + "/" + config['reference'],
        output_prefix = "TEMP/{unit}.unit",
        custom=config.get("params_bwa_mem", "")
        
    log: "TEMP/{unit}.unit.log"
    threads: 64
    shell: """
clearscratch
module load ea-utils/r822
R1=/scratch/`basename {input.fastq[0]}`
R2=/scratch/`basename {input.fastq[1]}`
mcf_log=/scratch/`basename {input.fastq[0]}`.mcf.log
fastq-mcf -C 1000000 -q 2 -p 10 -u -x 20 -o $R1 -o $R2 /home/zhujack/bin/adapters.fa <(gunzip -c {input.fastq[0]}) <(gunzip -c {input.fastq[1]}) > $mcf_log 2>&1

module load fastqc
fastqc -t {threads} $R1
fastqc -t {threads} $R2

module load bwa/{version}
bwa mem -M {params.custom} \
-R '@RG\tID:{wildcards.unit}\tSM:{params.sample}\tLB:{params.library}\tPL:{params.platform}' \
-t {threads} {params.bwa_index} $R1 $R2 2> {log} \
| samtools view -Sbh - \
| samtools sort -m 30000000000 - {params.output_prefix}

if [ ! -d fastqc/trimmed ]; then
    mkdir -p fastqc/trimmed
fi
mv /scratch/*fastqc.zip /scratch/*fastqc.html $mcf_log fastqc/trimmed/
"""

# RG="@RG\tID:${sample}\tLB:${sample}\tSM:${sample}\tPL:ILLUMINA"
# /usr/local/apps/bwa/0.7.10/bwa mem -M -t 32 -R "$RG" $ref ${sample}_R1.fastq.gz ${sample}_R2.fastq.gz >$workdir/${sample}.sam
# java -Xmx20g  -Djava.io.tmpdir=/scratch/ -jar /usr/local/apps/picard/1.119/SortSam.jar INPUT=$workdir/${sample}.sam OUTPUT=$dir/${sample}.bam SORT_ORDER=coordinate
# samtools index $dir/${sample}.bam


rule bwa_index_fasta:
    """Index fasta input for bwa.

    This rule uses config['references'][config['reference']] to get a specific fasta 
    file for indexing. The index is created prior to mapping anything.
    """
    input: config['references'][config['reference']]
    output: SERPENTINE_HOME + "/resources/mapping/bwaindex_" + config['bwaVersion'] + "/{reference}.pac"
    log: SERPENTINE_HOME + "/resources/mapping/bwaindex_" + config['bwaVersion'] + "/{reference}.log"
    version: config['bwaVersion']
    params: batch='-l nodes=1:gpfs',
        prefix=SERPENTINE_HOME + "/resources/mapping/bwaindex_" + config['bwaVersion'] + "/{reference}"
    shell: "module load bwa/{version} && bwa index -p {params.prefix} {input} 2> {log}"
